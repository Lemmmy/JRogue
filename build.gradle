plugins {
	id "com.github.johnrengelman.shadow" version "5.0.0"
	id "io.franzbecker.gradle-lombok" version "2.1"
	id "idea"
	id "application"
}

def jarName = "JRogue"
mainClassName = "jr.JRogue"
version = "alpha-0.4"

sourceCompatibility = 1.8

applicationDefaultJvmArgs = ["-XX:-UseAdaptiveSizePolicy", "-XX:+UseParallelGC"]

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

dependencies {
	implementation group: "org.apache.logging.log4j", name: "log4j-api", version: "2.11.2"
	implementation group: "org.apache.logging.log4j", name: "log4j-core", version: "2.11.2"

	implementation group: "commons-cli", name: "commons-cli", version: "1.4"
	implementation group: "org.apache.commons", name: "commons-lang3", version: "3.8.1"
	implementation group: "org.apache.commons", name: "commons-text", version: "1.6"

	implementation group: "com.github.alexeyr.pcg", name: "pcg", version: "0.9.1"
	implementation group: "org.ini4j", name: "ini4j", version: "0.5.4"
	implementation group: "org.reflections", name: "reflections", version: "0.9.11"
	implementation group: "com.google.code.gson", name: "gson", version: "2.8.5"

	implementation group: "org.fusesource.jansi", name: "jansi", version: "1.17.1"

	implementation group: "com.badlogicgames.gdx", name: "gdx", version: "1.9.9"
	implementation group: "com.badlogicgames.gdx", name: "gdx-freetype", version: "1.9.9"
	implementation group: "com.badlogicgames.gdx", name: "gdx-backend-lwjgl3", version: "1.9.9"
	implementation group: "com.badlogicgames.gdx", name: "gdx-platform", version: "1.9.9", classifier: "natives-desktop"
	implementation group: "com.badlogicgames.gdx", name: "gdx-freetype-platform", version: "1.9.9", classifier: "natives-desktop"

	implementation group: "ninja.leaping.configurate", name: "configurate-hocon", version: "3.3"
}

tasks.withType(JavaCompile) {
	options.incremental = true
}

jar {
	manifest { // TODO: this isn't being applied at runtime
		attributes(
			"Main-Class": mainClassName,
			"Version": project.version.toString(),
			"Build-Date": new Date().toString(),
			"Build-Revision": project.hasProperty("buildRevision") ? project.property("buildRevision") : "unknown",
			"Build-Branch": project.hasProperty("buildBranch") ? project.property("buildBranch") : "unknown",
			"Build-Number": project.hasProperty("buildNumber") ? project.property("buildNumber") : "-1"
		)
	}
}

shadowJar {
	baseName = jarName
	classifier = null
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint:-processing" << "-Xlint:-unchecked" << "-Xlint:-rawtypes" << "-Xmaxerrs" << "9999" << "-Xlint:deprecation"
	}
}

javadoc {
	source = sourceSets.main.allJava
	title = "JRogue API Documentation"
	destinationDir = project.hasProperty("javadocDir") ? file(project.property("javadocDir")) : file("docs/")
}